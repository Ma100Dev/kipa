*** Settings ***
Documentation   Testataan perustoiminnallisuudet
Suite Setup     Kipa Suite Setup
Suite Teardown  Stop Selenium Server
Test Setup      Open Competition
Test Teardown   Close Browser
Force Tags      regression  smoke
Library         SeleniumLibrary

*** Variables ***
${BROWSER}  Firefox
${KIPA MAIN PAGE URL}  http://localhost:8000/kipa/
${CREATE COMPETITION URL}  http://localhost:8000/kipa/uusiKisa/maarita/
${COMPETITION NAME}  ${EMPTY}

*** Test Cases ***
Luodaan uusi kisa
    [Setup]  Open Kipa Main Page
    Create new Competition  testi_kisa

Testaa syötä suorituksia
    Open Sub Page Verify Location And Title  syötä suorituksia  http://localhost:8000/kipa/testi_kisa/syota/  Kipa - Syötä tuloksia

Testaa syötä suorituksia - tarkistus
    Open Sub Page Verify Location And Title  syötä suorituksia - tarkistus  http://localhost:8000/kipa/testi_kisa/syota/tarkistus/  Kipa - Syötä tuloksia

Poista Luotu Kisa
    Remove Competition  ${COMPETITION NAME}

*** Keywords ***
Open KiPa Main Page
    Open Browser And Maximize  ${KIPA MAIN PAGE URL}
    Maximize Browser Window
    Title Should Be  Kipa - kaikki kisat

Create New Competition
    [Arguments]  ${competition}
    [Documentation]  Creates new competition and checks that it exist. Expects Competition name to be given as argument.
    Click Link  Luo uusi kisa
    Location Should Be  ${CREATE COMPETITION URL}
    Input Text  id_nimi  ${competition}
    Click Button  tallenna
    Close Browser
    Open Browser And Maximize  ${KIPA MAIN PAGE URL}
    Click Link  ${competition}
    Location Should Be  ${KIPA MAIN PAGE URL}${competition}/
    Title Should Be  Kipa - ${competition}
    Set Global Variable  ${COMPETITION NAME}  ${competition}

Open Browser and Maximize
    [Arguments]  ${page}
    Open Browser  ${page}  ${BROWSER}
    Maximize Browser Window

Kipa Suite Setup
    Start Selenium Server
    Open Kipa Main Page

Open Sub Page Verify Location And Title
    [Arguments]  ${link_name}  ${link}  ${page_title}
    Click Link  ${link_name}
    Location Should Be  ${link}
    Title Should Be  ${page_title}

Open Competition
    Open KiPa Main Page
    Click Link  ${COMPETITION NAME}
    Title Should Be  Kipa - ${COMPETITION NAME}

Remove Competition
    [Arguments]  ${competition}
    Open KiPa Main Page
    click link  ${competition}
    title should be  Kipa - ${competition}
    Click Link  poista kisa
    Title Should Be  Kipa - Poista kisa
    Click Button  Kyllä
    sleep   2 sec
    Title Should Be  Kipa - kaikki kisat
    Location Should Be  ${KIPA MAIN PAGE URL}

