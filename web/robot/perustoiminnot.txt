*** Settings ***
Documentation   Testataan perustoiminnallisuudet
Suite Setup     Kipa Suite Setup
Suite Teardown  Close All Browsers And Stop Selenium Server
Test Setup      Open Competition
Test Teardown   Close Browser
Force Tags      regression  smoke
Library         SeleniumLibrary

*** Test Cases ***
Luodaan uusi kisa
    [Setup]  Open Kipa Main Page
    Create new Competition  ${TESTIKISA}

Tarkista syötä suorituksia
    Open Sub Page Verify Location And Title  syötä suorituksia  ${TULOSTEN_SYOTTO_URL}  ${TULOSTEN_SYOTTO_OTSIKKO}

Tarkista syötä suorituksia - tarkistus
    Open Sub Page Verify Location And Title  syötä suorituksia - tarkistus  ${TULOSTEN_SYOTTO_TARKISTUS_URL}  ${TULOSTEN_SYOTTO_TARKISTUS_OTSIKKO}

Tarkista tulossivu
    Open Sub Page Verify Location And Title  sarjan tulokset  ${TULOSTEN_TARKISTUS_URL}  ${TULOSTEN_TARKISTUS_OTSIKKO}

Tarkista Tuomarineuvosto
    Open Sub Page Verify Location And Title  tuomarineuvoston työkalu  ${TUOMARINEUVOSTO_URL}  ${TUOMARINEUVOSTO_OTSIKKO}
    Page Should Contain Button  Peruuta
    Page Should Contain Button  tallenna

Tarkista laskennan tilanne
    Open Sub Page Verify Location And Title  laskennan tilanne  ${LASKENNAN_TILANNE_URL}  ${LASKENNAN_TILANNE_OTSIKKO}

Tarkista vartioiden määritys
    Open Sub Page Verify Location And Title  määrittele vartiot  ${VARTIOIDEN_MAARITYS_URL}  ${VARTIOIDEN_MAARITYS_OTSIKKO}
    Page Should Contain Button  Peruuta
    Page Should Contain Button  tallenna

Tarkista tehtävien määritys
    Open Sub Page Verify Location And Title  määrittele tehtävät  ${TEHTAVAN_MAARITYS_URL}  ${TEHTAVAN_MAARITYS_OTSIKKO}

Tarkista Testituloksien määritys
    Open Sub Page Verify Location And Title  määrittele testituloksia  ${TESTITULOKSIEN_MAARITYS_URL}  ${TESTITULOKSIEN_MAARITYS_OTSIKKO}
    Page Should Contain Button  Peruuta
    Page Should Contain Button  tallenna
    comment  Linkin, luo testitulokset sarjalle, pitäisi viedä samalle sivulle.
    Open Sub Page Verify Location And Title  luo testitulokset sarjalle  ${TESTITULOKSIEN_MAARITYS_URL}  ${TESTITULOKSIEN_MAARITYS_OTSIKKO}
    Page Should Contain Button  Peruuta
    Page Should Contain Button  tallenna

Tarkista palauta kisa
    Open Sub Page Verify Location And Title  palauta kisa  ${KISAN_TUONTI_URL}  ${KISAN_TUONTI_OTSIKKO}
    Page Should Contain Button  Peruuta
    Page Should Contain Button  tallenna
    Page Should Contain Element  file

Tarkista sarjan tulokset
    Open Sub Page Verify Location And Title  sarjan tulokset  ${TULOSTEN_TARKISTUS_URL}  ${TULOSTEN_TARKISTUS_OTSIKKO}

Lisätään sarjoja kisaan
    [Documentation]  sininen, harmaa ja valkoinen
    Click Link  määrittele kisan perustiedot
    Location Should Be  ${KIPA_URL}/${TESTIKISA}/${KISAN_MAARITYS_URL}/
    Title Should Be  Kipa - Määritä kisa
    Input Text  id_sarja_set-0-nimi  sininen
    Input Text  id_sarja_set-1-nimi  harmaa
    Input Text  id_sarja_set-2-nimi  valkoinen
    Click Button  tallenna
    Close Browser
    Open Competition
    Open Sub Page Verify Location And Title  määrittele vartiot  ${VARTIOIDEN_MAARITYS_URL}  ${VARTIOIDEN_MAARITYS_OTSIKKO}
    Page Should Contain Link  sininen
    Page Should Contain Link  harmaa
    Page Should Contain Link  valkoinen
    Capture Screenshot

Lisätään kaksi vartiota sarjaan
    [Documentation]  harmaa
    Open Sub Page Verify Location And Title  määrittele vartiot  ${VARTIOIDEN_MAARITYS_URL}  ${VARTIOIDEN_MAARITYS_OTSIKKO}
    comment  varmistetaan että käpistellään oikeaa sarjaa
    Run Keyword And Ignore Error  click link  harmaa
    Input Text  id_harmaa-0-nimi  testivartio1
    Input Text  id_harmaa-0-nro  100
    Input Text  id_harmaa-1-nimi  testivartio2
    Input Text  id_harmaa-1-nro  101
    click button  tallenna
    Close Browser
    Open Competition
    Open Sub Page Verify Location And Title  määrittele vartiot  ${VARTIOIDEN_MAARITYS_URL}  ${VARTIOIDEN_MAARITYS_OTSIKKO}
    Run Keyword And Ignore Error  click link  harmaa
    ${first_group}=  get value  id_harmaa-0-nimi
    Should Contain  ${first_group}  testivartio1
    ${second_group}=  get value  id_harmaa-1-nimi
    Should Contain  ${second_group}  testivartio2
    Capture Screenshot

Lisätään tehtäviä tyhjään sarjaan
    luo kisapistetehtävä  valkoinen

Lisätään tehtäviä vartioita sisältävään sarjaan
    luo kisapistetehtävä  harmaa

Poistetaan tyhjä sarja kisasta
    [Documentation]  sininen
    Click Link  määrittele kisan perustiedot
    Location Should Be  ${KIPA_URL}/${TESTIKISA}/${KISAN_MAARITYS_URL}/
    comment  Valitaan eka chekboxi, sininen
    Select Checkbox  id_sarja_set-0-DELETE
    Click Button  tallenna
    Close Browser
    Open Competition
    Open Sub Page Verify Location And Title  määrittele vartiot  ${VARTIOIDEN_MAARITYS_URL}  ${VARTIOIDEN_MAARITYS_OTSIKKO}
    Page Should Not Contain Link  sininen
    Page Should Contain Link  harmaa
    Page Should Contain Link  valkoinen
    Capture Screenshot

Poistetaan vartioita sisältävä sarja kisasta
    [Documentation]  harmaa
    Click Link  määrittele kisan perustiedot
    Location Should Be  ${KIPA_URL}/${TESTIKISA}/${KISAN_MAARITYS_URL}/
    comment  Valitaan eka chekboxi, harmaa
    Select Checkbox  id_sarja_set-0-DELETE
    Click Button  tallenna
    Close Browser
    Open Competition
    Open Sub Page Verify Location And Title  määrittele vartiot  ${VARTIOIDEN_MAARITYS_URL}  ${VARTIOIDEN_MAARITYS_OTSIKKO}
    Page Should Not Contain Link  harmaa
    Page Should Contain Link  valkoinen
    ${first_group}=  Run Keyword And Ignore Error  get value  id_harmaa-0-nimi
    Should not Contain  ${first_group}  testivartio1
    ${second_group}=  Run Keyword And Ignore Error  get value  id_harmaa-1-nimi
    Should not Contain  ${second_group}  testivartio2
    Capture Screenshot

Poista Luotu Kisa
    Remove Competition  ${TESTIKISA}
    [Teardown]  close all browsers

*** Keywords ***
Open KiPa Main Page
    Open Browser And Maximize  ${KIPA_URL}/
    Comment  Set Selenium Speed  2 sec
    Wait Until Keyword Succeeds  10 seconds  2 seconds  Title Should Be  ${KIPA_OTSIKKO}

Create New Competition
    [Arguments]  ${competition}
    [Documentation]  Creates new competition and checks that it exist. Expects TESTIKISA to be given as argument.
    Click Link  Luo uusi kisa
    Location Should Be  ${KIPA_URL}/uusiKisa/${KISAN_MAARITYS_URL}/
    Input Text  id_nimi  ${competition}
    Input Text  id_sarja_set-0-nimi  testisarja
    Click Button  tallenna
    Close Browser
    comment  kaikissa ympäristöissä ei näköjään ehditä avamaan selainta aikarajan puitteissa, yritetään 5 sekunnin välein 15 sekunnin ajan ennenkuin luovutetaan
    Wait Until Keyword Succeeds  15 seconds  5seconds  Open Browser And Maximize  ${KIPA_URL}/
    Click Link  ${competition}
    Location Should Be  ${KIPA_URL}/${competition}/
    Title Should Be  Kipa - ${competition}

Open Browser and Maximize
    [Arguments]  ${page}
    Open Browser  ${page}  ${BROWSER}
    Maximize Browser Window

Kipa Suite Setup
    Start Selenium Server
    comment  Uudelleenyritys testiserverin mahdollisen kuormituksen vuoksi.
    Wait Until Keyword Succeeds  3 min  1 min  Open Kipa Main Page
    Comment  Close Browser

Open Sub Page Verify Location And Title
    [Arguments]  ${link_name}  ${link}  ${page_title}
    Click Link  ${link_name}
    Wait Until Keyword Succeeds  10 second  2 second  Location Should Be  ${KIPA_URL}/${TESTIKISA}/${link}/
    Title Should Be  ${page_title}

Open Competition
    Open KiPa Main Page
    ${original timeout}=  Set Selenium Timeout  15 sec
    Click Link  ${TESTIKISA}
    Title Should Be  Kipa - ${TESTIKISA}

Remove Competition
    [Arguments]  ${competition}
    Open KiPa Main Page
    click link  ${competition}
    title should be  Kipa - ${competition}
    Click Link  poista kisa
    Title Should Be  ${KISAN_POISTO_OTSIKKO}
    Click Button  Kyllä
    sleep  2 sec
    Title Should Be  ${KAIKKI_KISAT_OTSIKKO}
    Location Should Be  ${KIPA_URL}/
    Page Should Not Contain Link  ${competition}
    Page Should Not Contain  ${competition}

Close All Browsers And Stop Selenium Server
    Close All Browsers
    Stop Selenium Server

luo kisapistetehtävä
    [Arguments]  ${sarja}
    Open Sub Page Verify Location And Title  määrittele tehtävät  ${TEHTAVAN_MAARITYS_URL}  ${TEHTAVAN_MAARITYS_OTSIKKO}
    Run Keyword And Ignore Error  Click Link  ${sarja}
    Click Link  Lisää uusi tehtävä
    Title Should Be  Kipa - Uusi tehtävä
    Input Text  tehtava_#1_nimi  kisapiste_testi_${sarja}
    Select Radio Button  tehtava_#1_#0_tyyppi  kp
    Input Text  tehtava_#1_#0_tyyppi_kp_a  testikuvaus
    click button  tallenna
    Wait Until Keyword Succeeds  10 sec  1 sec  Title Should Be  Kipa - kisapiste_testi_${sarja}
    Location Should Contain  talletettu
    Open Competition
    Open Sub Page Verify Location And Title  määrittele tehtävät  ${TEHTAVAN_MAARITYS_URL}  ${TEHTAVAN_MAARITYS_OTSIKKO}
    Run Keyword And Ignore Error  Click Link  ${sarja}
    Wait Until Keyword Succeeds  10 sec  1 sec  Page Should Contain  kisapiste_testi_${sarja}

